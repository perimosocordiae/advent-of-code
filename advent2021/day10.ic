-- ::= import "core.ic"
io ::= import "io.ic"
str ::= import "string/match.ic"
vec ::= import "lib/intvec.ic"
file ::= import "file.ic"
util ::= import "lib/util.ic"

ErrorScore ::= (c: char) -> i64 {
  if (c == !)) then { return 3 }
  if (c == !]) then { return 57 }
  if (c == !}) then { return 1197 }
  if (c == !>) then { return 25137 }
  return -1
}

CompletionScore ::= (c: char) -> i64 {
  if (c == !)) then { return 1 }
  if (c == !]) then { return 2 }
  if (c == !}) then { return 3 }
  if (c == !>) then { return 4 }
  return -1
}

line_result ::= struct {
  corrupt: i64
  incomplete: i64
}

ProcessLine ::= (line: []char) -> line_result {
  open_stack := vec.empty(10)
  for (0, line.length as i64 - 1) do [i: i64] {
    idx := str.IndexOf("({[<", line[i])
    if (idx < 4) then {
      open_stack'append(")}]>"[idx] as i64)
    } else {
      if (open_stack.length > 0 and
          open_stack'back() == (line[i] as i64)) then {
        open_stack'pop_back()
      } else {
        return line_result.{ corrupt = ErrorScore(line[i]) \\ incomplete = 0 }
      }
    }
  }
  num_open := open_stack.length as i64
  incomplete_score := 0
  vec.ReverseForEach(open_stack) do [ch: i64] {
    incomplete_score *= 5
    incomplete_score += CompletionScore(ch as i8 as char)
  }
  return line_result.{ corrupt = 0 \\ incomplete = incomplete_score }
}

Solve ::= (filename: []char) -> () {
  corrupted_score := 0
  incomplete_scores := vec.empty(5)
  file.With(filename) open [f: file.File] {
    file.Lines(f) each [line: []char] {
      result := ProcessLine(line)
      corrupted_score += result.corrupt
      if (result.incomplete > 0) then {
        incomplete_scores'append(result.incomplete)
      }
    }
  }
  io.Print("Part 1: ", corrupted_score, !\n)

  incomplete_scores'sort()
  mid := incomplete_scores.length as i64 / 2
  io.Print("Part 2: ", incomplete_scores[mid], !\n)
}

io.Print("Test:\n")
Solve("inputs/10.test")

io.Print("Full:\n")
Solve("inputs/10.full")
