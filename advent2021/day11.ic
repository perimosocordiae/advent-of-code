-- ::= import "core.ic"
io ::= import "io.ic"
str ::= import "string/match.ic"
vec ::= import "lib/intvec.ic"
file ::= import "file.ic"
util ::= import "lib/util.ic"

IncrementIfNonzero ::= (x: *i64) -> () {
  if (@x != 0) {
    @x += 1
  }
}

Solve ::= (filename: []char) -> bool {
  grid: [12, 12; i64]
  file.With(filename) open [f: file.File] {
    row := 1
    file.Lines(f) each [line: []char] {
      for (1, 11) do [col: i64] {
        grid[row][col] = util.ParseInt64(line[col - 1])
      }
      row += 1
    }
  }

  // Simulate
  total_flashes := 0
  for (0, 10000) do [step: i64] {
    // All cells increase by 1
    for (1, 11) do [row: i64] {
      for (1, 11) do [col: i64] {
        grid[row][col] += 1
      }
    }
    // Process flashes (energy > 9)
    step_flashes := 0
    dirty_bit := true
    while (dirty_bit) {
      dirty_bit = false
      for (1, 11) do [row: i64] {
        for (1, 11) do [col: i64] {
          if (grid[row][col] > 9) {
            grid[row][col] = 0
            step_flashes += 1
            dirty_bit = true
            IncrementIfNonzero(&grid[row - 1][col - 1])
            IncrementIfNonzero(&grid[row - 1][col])
            IncrementIfNonzero(&grid[row - 1][col + 1])
            IncrementIfNonzero(&grid[row][col - 1])
            IncrementIfNonzero(&grid[row][col + 1])
            IncrementIfNonzero(&grid[row + 1][col - 1])
            IncrementIfNonzero(&grid[row + 1][col])
            IncrementIfNonzero(&grid[row + 1][col + 1])
          }
        }
      }
    }
    total_flashes += step_flashes
    if (step == 99) {
      io.Print("Part 1: ", total_flashes, !\n)
    }
    if (step_flashes == 100) {
      io.Print("Part 2: ", step + 1, !\n)
      return true
    }
  }
  return false
}

io.Print("Test:\n")
Solve("inputs/11.test")

io.Print("Full:\n")
Solve("inputs/11.full")
